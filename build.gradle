buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
}
plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {

  // Mailet
  compile "org.apache.james:apache-mailet-base:$apacheJamesVersion"
  compile "org.apache.james:james-server-util:$apacheJamesVersion"
  compile "javax.inject:javax.inject:1"
  
  testCompile "com.h2database:h2:1.4.197"
  testCompile "junit:junit:4.12"
  testCompile "org.springframework:spring-jdbc:5.0.5.RELEASE"
  testCompile "org.springframework:spring-test:5.0.5.RELEASE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:james-extra-components'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/james-extra-components'
  }

  publications = ['MyPublication']
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'com.foilen'
      artifactId 'james-extra-components'
    }
  }
}

task wrapper(type: Wrapper) { gradleVersion = '4.1' }
